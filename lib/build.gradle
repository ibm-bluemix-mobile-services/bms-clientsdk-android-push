apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'

    options.links 'http://docs.oracle.com/javase/7/docs/api/'
    options.encoding 'UTF-8'
    options.memberLevel = JavadocMemberLevel.PUBLIC

    setTitle 'Bluemix Mobile Services Push Android API'

    failOnError false
}

task androidJavadocsJar(type: Jar, dependsOn: generateJavadoc) {
    classifier ='javadoc'
    from generateJavadoc.destinationDir
}

task androidSourcesJar(type: Jar){
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

signing {
    required { isReleaseBuild() }
    sign configurations.archives
}

def getRepositoryUsername() {
    return hasProperty('ossrhUsername') ? ossrhUsername :''
}

def getRepositoryPassword() {
    return hasProperty('ossrhPassword') ? ossrhPassword : ''
}

def getReleaseRepositoryUrl() {
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}

def getSnapshotRepositoryUrl() {
    return 'https://oss.sonatype.org/content/repositories/snapshots/'
}

def isReleaseBuild() {
    return !getVersion().contains('-SNAPSHOT')
}

def getVersion() {
    return '2.0.2-SNAPSHOT'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            snapshotRepository(url: getSnapshotRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId='com.ibm.mobilefirstplatform.clientsdk.android'
            pom.artifactId='push'
            pom.version = getVersion()

            pom.project {
                name 'IBM Bluemix Mobile Services - Client SDK Android Push'
                description 'This is the Push component of the Android SDK for IBM Bluemix Mobile Services.'
                url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-push'

                scm {
                    connection 'scm:git:git@github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-push.git'
                    developerConnection 'scm:git:git@github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-push.git'
                    url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-push'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'IBM Bluemix Services Mobile SDK'
                        email 'mobilsdk@us.ibm.com'
                    }
                }
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-gcm:9.0.1'

    compile group: 'com.ibm.mobilefirstplatform.clientsdk.android',
            name: 'core',
            version: '2.0.1',
            ext: 'aar',
            transitive: true
}
